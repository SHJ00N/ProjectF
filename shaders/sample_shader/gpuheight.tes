#version 460 core
layout(quads, fractional_odd_spacing, ccw) in;

layout (std140, binding = 0) uniform CameraBlock
{
    mat4 projection;
    mat4 view;
    vec3 cameraPos;
};

uniform sampler2D heightMap;
uniform mat4 model;
uniform float heightScale;

in vec2 TextureCoord[];

out vec2 TexCoords;
out mat3 TBN;
out vec3 ViewPos;
out vec3 FragPos;

void main()
{
    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;

    vec2 t00 = TextureCoord[0];
    vec2 t01 = TextureCoord[1];
    vec2 t10 = TextureCoord[2];
    vec2 t11 = TextureCoord[3];

    vec2 t0 = (t01 - t00) * u + t00;
    vec2 t1 = (t11 - t10) * u + t10;
    vec2 texCoord = (t1 - t0) * v + t0;

    float Height = texture(heightMap, texCoord).r * heightScale;

    vec4 p00 = gl_in[0].gl_Position;
    vec4 p01 = gl_in[1].gl_Position;
    vec4 p10 = gl_in[2].gl_Position;
    vec4 p11 = gl_in[3].gl_Position;

    vec4 p0 = (p01 - p00) * u + p00;
    vec4 p1 = (p11 - p10) * u + p10;
    vec4 p = (p1 - p0) * v + p0;

    p.y += Height;

    float hL = texture(heightMap, texCoord + vec2(-1, 0)).r;
    float hR = texture(heightMap, texCoord + vec2( 1, 0)).r;
    float hD = texture(heightMap, texCoord + vec2(0, -1)).r;
    float hU = texture(heightMap, texCoord + vec2(0,  1)).r;

    mat3 normalMatrix = transpose(inverse(mat3(model)));
    vec3 N = normalize(normalMatrix * vec3((hL - hR) * heightScale, 2.0, (hD - hU) * heightScale));
    vec3 T = normalize(normalMatrix * vec3(1.0, 0.0, 0.0));
    vec3 B = normalize(cross(N, T));
    T = normalize(cross(B, N));

    TBN = mat3(T, B, N);
    TexCoords = texCoord;
    ViewPos = cameraPos;
    FragPos = vec3(model * p); 

    gl_Position = projection * view * model * p;
}